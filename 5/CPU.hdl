// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15], out=noti);
    Or(a=noti, b=instruction[5], out=Aload);
    Mux16(a=ALUop, b=instruction, sel=noti, out=toA);
    ARegister(in=toA, load=Aload, out=Aout, out[0..14]=addressM);
    And(a=instruction[4], b=instruction[15], out=Dload);
    DRegister(in=ALUop, load=Dload, out=toALUa);
    Mux16(a=Aout, b=inM, sel=instruction[12], out=toALUb);

    And(a=instruction[15], b=instruction[3], out=writeM);

    And(a=instruction[2], b=N, out=pcIa);
    And(a=instruction[1], b=Z, out=pcIb);

    Not(in=N, out=notN);
    Not(in=Z, out=notZ);
    And(a=notN, b=notZ, out=P);
    And(a=instruction[0], b=P, out=pcIc);
    Or(a=pcIa, b=pcIb, out=or12);
    Or(a=pcIc, b=or12, out=condcheck);
    And(a=condcheck, b=instruction[15], out=JUMP);

    Not(in=condcheck, out=notcondcheck);
    Or(a=notcondcheck, b=noti, out=pcinc);

    PC(in=Aout, load=JUMP, inc=pcinc, reset=reset, out[0..14]=pc);
    ALU(x=toALUa ,y=toALUb ,zx=instruction[11] ,nx=instruction[10] ,zy=instruction[9] ,ny=instruction[8] ,f=instruction[7] ,no=instruction[6] ,out=outM, out=ALUop, zr=Z ,ng=N);
}
